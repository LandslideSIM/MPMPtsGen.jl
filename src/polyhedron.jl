#==========================================================================================+
|        MaterialPointGenerator.jl: Generate structured material particles in Julia        |
+------------------------------------------------------------------------------------------+
|  File Name  : polyhedron.jl                                                              |
|  Description: Generate structured mesh in 3D space by a given polyhedron                 |
|  Programmer : Zenan Huo                                                                  |
|  Start Date : 01/01/2022                                                                 |
|  Affiliation: Risk Group, UNIL-ISTE                                                      |
|  Functions  : 01. readmsh                                                                |
|               02. polyhedron2particle                                                    |
|               03. trimesh_voxelize                                                       |
+==========================================================================================#

export polyhedron2particle

"""
    readmsh(msh_path::String; precision="FP32")

Description:
---
Read the mesh file (`.msh`) generated by Gmsh. The function will return the vertices and 
faces for triangle. It's necessary to check the mesh qulity before using this function. 
You can do it in MeshLab or Gmsh GUI. `precision` can be "FP64" or "FP32" in `String`.
"""
function readmsh(msh_path; precision="FP32")
    T1 = precision == "FP32" ?   Int32 :   Int64
    T2 = precision == "FP32" ? Float32 : Float64
    # launch gmsh and load msh file
    gmsh.initialize()
    gmsh.option.setNumber("General.Terminal", 0)
    gmsh.open(msh_path)
    # get mesh data
    node_tags, node_coords, _ = gmsh.model.mesh.getNodes()
    num_nodes = length(node_tags)
    vertices = reshape(node_coords, 3, num_nodes)'
    element_types, element_tags, element_node_tags = gmsh.model.mesh.getElements()
    boundary = gmsh.model.getBoundary([], true, true, true)
    isempty(boundary) || error("3D object mesh is not closed.")
    gmsh.finalize()
    # generate vertices and faces in julia array
    triangle_type = 2
    triangle_index = findfirst(==(triangle_type), element_types)
    isnothing(triangle_index) && error("No triangles found in the mesh.")
    triangle_node_tags = element_node_tags[triangle_index]
    num_triangles = length(triangle_node_tags) รท 3
    faces = reshape(T1.(triangle_node_tags), 3, num_triangles)'
    return Array{T2, 2}(vertices), Array{T1, 2}(faces)
end

"""
    polyhedron2particle(stl_file::String, output_file, lp; verbose::Bool=false)

Description:
---
Convert a polyhedron (`.stl`) to a set of particles. The function will write the populated 
particles of each voxel into a `.xyz` file. The voxel size is defined by `lp`, it is suggest
to be equal to the MPM background grid size. The `verbose` is a flag to show the time 
consumption of each step.

Example:
---
```julia
polyhedron2particle("/path/to/yout/model.stl", "/path/to/model.xyz", 0.1, verbose=true)
```
"""
function polyhedron2particle(stl_file::String, output_file::String, lp; verbose::Bool=false)
    pts_center, tp = trimesh_voxelize(stl_file, lp)
    offset = lp * 0.25
    offsets = np[].array([[-offset,  offset,  offset], [-offset, -offset,  offset],
                          [ offset,  offset,  offset], [ offset, -offset,  offset],
                          [-offset,  offset, -offset], [-offset, -offset, -offset],
                          [ offset,  offset, -offset], [ offset, -offset, -offset]])
    pts = np[].vstack([np[].add(pts_center, offset) for offset in offsets])
    t4_start = time()
    np[].savetxt(output_file, pts, fmt="%.6f", delimiter=" ")
    t4_end = time(); t4 = t4_end - t4_start
    if verbose
        tt = sum(tp) + t4
        @info """voxelization with trimesh
        - load model  : $(@sprintf("%6.2f", tp[1])) s | $(@sprintf("%6.2f", 100*tp[1]/tt))%
        - voxelize    : $(@sprintf("%6.2f", tp[2])) s | $(@sprintf("%6.2f", 100*tp[2]/tt))%
        - fill voxels : $(@sprintf("%6.2f", tp[3])) s | $(@sprintf("%6.2f", 100*tp[3]/tt))%
        - write .xyz  : $(@sprintf("%6.2f", t4)) s | $(@sprintf("%6.2f", 100*t4/tt))%
        $("-"^34)
        - total time  : $(@sprintf("%6.2f", sum(tp))) s
        """
    end
    return nothing
end

"""
    trimesh_voxelize(stl_file::String, lp)

Description:
---
Voxelize the given STL model with the trimesh package. The voxel size is defined by `lp`.
"""
function trimesh_voxelize(stl_file::String, lp)
    lp > 0 || throw(ArgumentError("lp must be positive"))
    t1_start = time(); mesh = trimesh[].load(stl_file, process=true); t1_end = time()
    @info "STL model loaded"
    t2_start = time(); voxelized = voxelize(mesh, lp); t2_end = time()
    @info "voxelized with trimesh"
    t3_start = time(); voxelized_filled = voxelized.fill(); t3_end = time()
    points = voxelized_filled.points
    pts_num = pyconvert(Int, points.shape[0])
    @info "filled with $(pts_num * 8) particles"
    t1 = t1_end - t1_start
    t2 = t2_end - t2_start
    t3 = t3_end - t3_start
    return points, [t1, t2, t3]
end